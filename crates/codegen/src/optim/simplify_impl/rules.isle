(type
  SimplifyRawResult
    (enum
      (Value (val Value))
      (Expr (expr ExprData))
    )
)


(extern const $true bool)
(extern const $false bool)

(decl is_zero (bool) ExprValue)
(extern extractor infallible is_zero is_zero)

(decl is_one (bool) ExprValue)
(extern extractor infallible is_one is_one)

(decl is_two (bool) ExprValue)
(extern extractor infallible is_two is_two)

(decl is_all_one (bool) ExprValue)
(extern extractor infallible is_all_one is_all_one)

(decl is_eq (bool ExprValue) ExprValue)
(extern extractor infallible is_eq is_eq (out in))

(decl is_power_of_two (bool) ExprValue)
(extern extractor infallible is_power_of_two is_power_of_two)

(decl make_zero (Type) ExprValue)
(extern constructor make_zero make_zero)

(decl make_one (Type) ExprValue)
(extern constructor make_one make_one)

(decl make_all_one (Type) ExprValue)
(extern constructor make_all_one make_all_one)

(decl make_result (ExprValue) SimplifyRawResult)
(extern constructor make_result make_result)


;; Insn Simplify rule ;;
(decl simplify (Expr) SimplifyRawResult)

;; Rules for BinaryOp.Add ;;
; a + 0 => a
(rule
  (simplify
    (extract_add a (is_zero $true)))
  (make_result a)
)
; a + (-a) => 0
(rule
  (simplify
    (extract_add a (value_expr (extract_neg (value_with_ty(is_eq $true <a) ty))))
  )
  (make_result (make_zero ty))
)
; a + (b - a) => b
(rule
  (simplify
    (extract_add a (value_expr (extract_sub b (is_eq $true <a))))
  )
  (make_result b)
)
; (a - b) + b => a
(rule
  (simplify
    (extract_add (value_expr (extract_sub a b)) (is_eq $true <b))
  )
  (make_result a)
)
; a + !a => -1
(rule
  (simplify
    (extract_add a (value_expr (extract_not (value_with_ty (is_eq $true <a) ty))))
  )
  (make_result (make_all_one ty))
)

;; Rules for BinaryOp.Sub ;;
; a - 0 => a
(rule
  (simplify
    (extract_sub a (is_zero $true))
  )
  (make_result a)
)
; 0 - a => -a
(rule
  (simplify
    (extract_sub (is_zero $true) a)
  )
  (SimplifyRawResult.Expr (ExprData.Unary (UnaryOp.Neg) (arg_array1 a)))
)
; a - a => 0
(rule
  (simplify
    (extract_sub a (value_with_ty (is_eq $true <a) ty))
  )
  (make_result (make_zero ty))
)
; a - (a - b) =>  b
(rule
  (simplify
    (extract_sub a (value_expr (extract_sub (is_eq $true <a) b)))
  )
  (make_result b)
)
; (a - b) - a => -b
(rule
  (simplify
    (extract_sub (value_expr (extract_sub a b)) (is_eq $true <a))
  )
  (SimplifyRawResult.Expr (ExprData.Unary (UnaryOp.Neg) (arg_array1 b)))
)
; a - (a + b) => -b
(rule
  (simplify
    (extract_sub a (value_expr (extract_add (is_eq $true <a) b)))
  )
  (SimplifyRawResult.Expr (ExprData.Unary (UnaryOp.Neg) (arg_array1 b)))
)
; b - (a + b) => -a
(rule
  (simplify
    (extract_sub b (value_expr (extract_add a (is_eq $true <b))))
  )
  (SimplifyRawResult.Expr (ExprData.Unary (UnaryOp.Neg) (arg_array1 a)))
)
; (a + b) - a =>  b
(rule
  (simplify
    (extract_sub (value_expr (extract_add a b)) (is_eq $true <a))
  )
  (make_result b)
)
; (a + b) - b =>  a
(rule
  (simplify
    (extract_sub (value_expr (extract_add a b)) (is_eq $true <b))
  )
  (make_result a)
)

;; Rules for BinaryOp.Mul ;;
; _ * 0 => 0
(rule
  (simplify
    (extract_mul _ (value_with_ty (is_zero $true) ty))
  )
  (make_result (make_zero ty))
)
; a * 1 => a
(rule
  (simplify
    (extract_mul a (is_one $true))
  )
  (make_result a)
)
; a * 2 => a + a
(rule
  (simplify
    (extract_mul a (is_two $true))
  )
  (SimplifyRawResult.Expr (ExprData.Binary (BinaryOp.Add) (arg_array2 a a)))
)

;; Rules for BinaryOp.Sdiv ;;
;; We don't simplify patterns where a divisor is a value not to change semantics. ;;
; 0 / a => 0
(rule
  (simplify
    (extract_sdiv (value_with_ty (is_zero $true) ty) _)
  )
  (make_result (make_zero ty))
)

;; Rules for BinaryOp.Udiv ;;
;; We don't simplify patterns where a divisor is a value not to change semantics. ;;
; 0 / a => 0
(rule
  (simplify
    (extract_udiv (value_with_ty (is_zero $true) ty) _)
  )
  (make_result (make_zero ty))
)

;; Rules for BinaryOp.Lt
; a < a => 0
(rule
  (simplify
    (extract_lt a (value_with_ty (is_eq $true <a) ty))
  )
  (make_result (make_zero ty))
)

;; Rules for BinaryOp.Gt
; a > b => b < a
(rule
  (simplify
    (extract_gt a b)
  )
  (SimplifyRawResult.Expr (ExprData.Binary (BinaryOp.Lt) (arg_array2 b a)))
)

;; Rules for BinaryOp.Slt
; a < a => 0
(rule
  (simplify
    (extract_slt a (value_with_ty (is_eq $true <a) ty))
  )
  (make_result (make_zero ty))
)

;; Rules for BinaryOp.Sgt
; a > b => b < a
(rule
  (simplify
    (extract_sgt a b)
  )
  (SimplifyRawResult.Expr (ExprData.Binary (BinaryOp.Slt) (arg_array2 b a)))
)

;; Rules for BinaryOp.Le
; a <= a => 1
(rule
  (simplify
    (extract_le a (value_with_ty (is_eq $true <a) ty))
  )
  (make_result (make_all_one ty))
)

;; Rules for BinaryOp.Ge
; a >= b => b <= a
(rule
  (simplify
    (extract_ge a b)
  )
  (SimplifyRawResult.Expr (ExprData.Binary (BinaryOp.Le) (arg_array2 b a)))
)

;; Rules for BinaryOp.Sle
; a <= a => 1
(rule
  (simplify
    (extract_sle a (value_with_ty (is_eq $true <a) ty))
  )
  (make_result (make_all_one ty))
)

;; Rules for BinaryOp.Sge
; a <= b => b <= a
(rule
  (simplify
    (extract_sge a b)
  )
  (SimplifyRawResult.Expr (ExprData.Binary (BinaryOp.Sle) (arg_array2 b a)))
)

;; Rules for BinaryOp.Eq
; a == a => 1
(rule
  (simplify
    (extract_eq a (value_with_ty (is_eq $true <a) ty))
  )
  (make_result (make_all_one ty))
)

;; Rules for BinaryOp.Ne
; a != a => 0
(rule
  (simplify
    (extract_ne a (value_with_ty (is_eq $true <a) ty))
  )
  (make_result (make_zero ty))
)

;; Rules for BinaryOp.And
; a & a => a
(rule
  (simplify
    (extract_and a (is_eq $true <a))
  )
  (make_result a)
)
; _ & 0 => 0
(rule
  (simplify
    (extract_and _ (value_with_ty (is_zero $true) ty))
  )
  (make_result (make_zero ty))
)
; a & -1 => a
(rule
  (simplify
    (extract_and a (is_all_one $true))
  )
  (make_result a)
)
; a & !a => 0
(rule
  (simplify
    (extract_and a (value_expr (extract_not (value_with_ty (is_eq $true <a) ty))))
  )
  (make_result (make_zero ty))
)
; a & (a | _) => a
(rule
  (simplify
    (extract_and a (value_expr (extract_or (is_eq $true <a) _)))
  )
  (make_result a)
)
; a & (_ | a) => a
(rule
  (simplify
    (extract_and a (value_expr (extract_or _ (is_eq $true <a))))
  )
  (make_result a)
)
; (a | b) & (a | !b) => a
(rule
  (simplify
    (extract_and
      (value_expr (extract_or a b))
      (value_expr
        (extract_or (is_eq $true <a) (value_expr (extract_not (is_eq $true <b))))
      )
    )
  )
  (make_result a)
)
; (a | b) & (!b | a) => a
(rule
  (simplify
    (extract_and
      (value_expr (extract_or a b))
      (value_expr
        (extract_or (value_expr (extract_not (is_eq $true <b))) (is_eq $true <a))
      )
    )
  )
  (make_result a)
)
; (a | b) & (!a | b) => b
(rule
  (simplify
    (extract_and
      (value_expr (extract_or a b))
      (value_expr
        (extract_or (value_expr (extract_not (is_eq $true <a))) (is_eq $true <b))
      )
    )
  )
  (make_result b)
)
; (a | b) & (b | !a) => b
(rule
  (simplify
    (extract_and
      (value_expr (extract_or a b))
      (value_expr
        (extract_or (is_eq $true <b) (value_expr (extract_not (is_eq $true <a))))
      )
    )
  )
  (make_result b)
)
; a & (-a) => a where a: is_power_of_two.
(rule
  (simplify
    (extract_and a (value_expr (extract_neg (and (is_eq $true <a) (is_power_of_two $true)))))
  )
  (make_result a)
)
; a & (a - 1) => 0 where a: is_power_of_two.
(rule
  (simplify
    (extract_and a (value_expr (extract_sub (and (is_eq $true <a) (is_power_of_two $true) (value_ty ty)) (is_one $true))))
  )
  (make_result (make_zero ty))
)

;; Rules for BinaryOp.Or
; a | -1 => -1
(rule
  (simplify
    (extract_or a (value_with_ty (is_all_one $true) ty))
  )
  (make_result (make_all_one ty))
)
; a | a => a
(rule
  (simplify
    (extract_or a (is_eq $true <a))
  )
  (make_result a)
)
; a | 0 => a
(rule
  (simplify
    (extract_or a (is_zero $true))
  )
  (make_result a)
)
; a | !a => -1
(rule
  (simplify
    (extract_or a (value_expr (extract_not (value_with_ty (is_eq $true <a) ty))))
  )
  (make_result (make_all_one ty))
)
; a | (a & _) => a
(rule
  (simplify
    (extract_or a (value_expr (extract_and (is_eq $true <a) _)))
  )
  (make_result a)
)
; a | (_ & a) => a
(rule
  (simplify
    (extract_or a (value_expr (extract_and _ (is_eq $true <a))))
  )
  (make_result a)
)
; a | !(a & _) => -1
(rule
  (simplify
    (extract_or a (value_expr (extract_not (value_expr (extract_and (value_with_ty (is_eq $true <a) ty) _)))))
  )
  (make_result (make_all_one ty))
)
; a | !(_ & a) => -1
(rule
  (simplify
    (extract_or a (value_expr (extract_not (value_expr (extract_and _ (value_with_ty (is_eq $true <a) ty))))))
  )
  (make_result (make_all_one ty))
)
; (a ^ b) | (a & !b) => (a ^ b)
(rule
  (simplify
    (extract_or
      arg0 @ (value_expr (extract_xor a b))
      (value_expr
        (extract_and (is_eq $true <a) (value_expr (extract_not (is_eq $true <b))))
      )
    )
  )
  (make_result arg0)
)
; (a ^ b) | (!b & a) => (a ^ b)
(rule
  (simplify
    (extract_or
      arg0 @ (value_expr (extract_xor a b))
      (value_expr
        (extract_and (value_expr (extract_not (is_eq $true <b))) (is_eq $true <a))
      )
    )
  )
  (make_result arg0)
)
; (a ^ b) | (!a & b) => (a ^ b)
(rule
  (simplify
    (extract_or
      arg0 @ (value_expr (extract_xor a b))
      (value_expr
        (extract_and (value_expr (extract_not (is_eq $true <a))) (is_eq $true <b))
      )
    )
  )
  (make_result arg0)
)
; (a ^ b) | (b & !a) => (a ^ b)
(rule
  (simplify
    (extract_or
      arg0 @ (value_expr (extract_xor a b))
      (value_expr
        (extract_and (is_eq $true <b) (value_expr (extract_not (is_eq $true <a))))
      )
    )
  )
  (make_result arg0)
)
; (!a ^ b) | (a & b) => (!a ^ b)
(rule
  (simplify
    (extract_or
      arg0 @ (value_expr (extract_xor (value_expr (extract_not a)) b))
      (value_expr
        (extract_and (is_eq $true <a) (is_eq $true <b))
      )
    )
  )
  (make_result arg0)
)
; (!a ^ b) | (b & a) => (!a ^ b)
(rule
  (simplify
    (extract_or
      arg0 @ (value_expr (extract_xor (value_expr (extract_not a)) b))
      (value_expr
        (extract_and (is_eq $true <b) (is_eq $true <a))
      )
    )
  )
  (make_result arg0)
)
; (a ^ !b) | (a & b) => (a ^ !b)
(rule
  (simplify
    (extract_or
      arg0 @ (value_expr (extract_xor a (value_expr (extract_not b))))
      (value_expr
        (extract_and (is_eq $true <a) (is_eq $true <b))
      )
    )
  )
  (make_result arg0)
)
; (a ^ !b) | (b & a) => (a ^ !b)
(rule
  (simplify
    (extract_or
      arg0 @ (value_expr (extract_xor a (value_expr (extract_not b))))
      (value_expr
        (extract_and (is_eq $true <b) (is_eq $true <a))
      )
    )
  )
  (make_result arg0)
)
; (a | b) | (a ^ b) => (a | b)
(rule
  (simplify
    (extract_or
      arg0 @ (value_expr (extract_or a b))
      (value_expr
        (extract_xor (is_eq $true <a) (is_eq $true <b))
      )
    )
  )
  (make_result arg0)
)
; (a | b) | (b ^ a) => (a | b)
(rule
  (simplify
    (extract_or
      arg0 @ (value_expr (extract_or a b))
      (value_expr
        (extract_xor (is_eq $true <b) (is_eq $true <a))
      )
    )
  )
  (make_result arg0)
)
; (!a & b) | !(a | b) => !a
(rule
  (simplify
    (extract_or
       (value_expr (extract_and neg_a @ (value_expr (extract_not a)) b))
       (value_expr (extract_not (value_expr (extract_or (is_eq $true <a) (is_eq $true <b)))))
    )
  )
  (make_result neg_a)
)
; (!a & b) | !(b | a) => !a
(rule
  (simplify
    (extract_or
       (value_expr (extract_and neg_a @ (value_expr (extract_not a)) b))
       (value_expr (extract_not (value_expr (extract_or (is_eq $true <b) (is_eq $true <a)))))
    )
  )
  (make_result neg_a)
)
; (a & !b) | !(a | b) => !b
(rule
  (simplify
    (extract_or
       (value_expr (extract_and a neg_b @ (value_expr (extract_not b))))
       (value_expr (extract_not (value_expr (extract_or (is_eq $true <a) (is_eq $true <b)))))
    )
  )
  (make_result neg_b)
)
; (a & !b) | !(b | a) => !b
(rule
  (simplify
    (extract_or
       (value_expr (extract_and a neg_b @ (value_expr (extract_not b))))
       (value_expr (extract_not (value_expr (extract_or (is_eq $true <b) (is_eq $true <a)))))
    )
  )
  (make_result neg_b)
)

;; Rules for BinaryOp.Xor
; a ^ 0 => a
(rule
  (simplify
    (extract_xor a (is_zero $true))
  )
  (make_result a)
)
; a ^ a => 0
(rule
  (simplify
    (extract_xor a (value_with_ty (is_eq $true <a) ty))
  )
  (make_result (make_zero ty))
)
; a ^ !a => -1
(rule
  (simplify
    (extract_xor a (value_expr_with_ty (extract_not (is_eq $true <a)) ty))
  )
  (make_result (make_all_one ty))
)
; a ^ (a ^ b) => b
(rule
  (simplify
    (extract_xor a (value_expr (extract_xor (is_eq $true <a) b)))
  )
  (make_result b)
)
; a ^ (b ^ a) => b
(rule
  (simplify
    (extract_xor a (value_expr (extract_xor b (is_eq $true <a))))
  )
  (make_result b)
)
; (a | b) ^ (!a & b) => a
(rule
  (simplify
    (extract_xor
      (value_expr (extract_or a b))
      (value_expr (extract_and (value_expr (extract_not (is_eq $true <a))) (is_eq $true <b)))
    )
  )
  (make_result a)
)
; (a | b) ^ (b & !a) => a
(rule
  (simplify
    (extract_xor
      (value_expr (extract_or a b))
      (value_expr (extract_and (is_eq $true <b) (value_expr (extract_not (is_eq $true <a)))))
    )
  )
  (make_result a)
)
; (a | b) ^ (a & !b) => b
(rule
  (simplify
    (extract_xor
      (value_expr (extract_or a b))
      (value_expr (extract_and (is_eq $true <a) (value_expr (extract_not (is_eq $true <b)))))
    )
  )
  (make_result b)
)
; (a | b) ^ (!b & a) => b
(rule
  (simplify
    (extract_xor
      (value_expr (extract_or a b))
      (value_expr (extract_and (value_expr (extract_not (is_eq $true <b))) (is_eq $true <a)))
    )
  )
  (make_result b)
)

; (a & b) ^ (!a | b) => !a
(rule
  (simplify
    (extract_xor
      (value_expr (extract_and a b))
      (value_expr (extract_or not_a @ (value_expr (extract_not (is_eq $true <a))) (is_eq $true <b)))
    )
  )
  (make_result not_a)
)
; (a & b) ^ (b | !a) => !a
(rule
  (simplify
    (extract_xor
      (value_expr (extract_and a b))
      (value_expr (extract_or (is_eq $true <b) not_a @ (value_expr (extract_not (is_eq $true <a)))))
    )
  )
  (make_result not_a)
)
; (a & b) ^ (a | !b) => !b
(rule
  (simplify
    (extract_xor
      (value_expr (extract_and a b))
      (value_expr (extract_or (is_eq $true <a) not_b @ (value_expr (extract_not (is_eq $true <b)))))
    )
  )
  (make_result not_b)
)
; (a & b) ^ (!b | a) => !b
(rule
  (simplify
    (extract_xor
      (value_expr (extract_and a b))
      (value_expr (extract_or not_b @ (value_expr (extract_not (is_eq $true <b))) (is_eq $true <a)))
    )
  )
  (make_result not_b)
)

;; Rules for UnaryOp.Not
; !(!a) => a
(rule
  (simplify
    (extract_not (value_expr (extract_not a)))
  )
  (make_result a)
)
; !(-a) => a - 1
(rule
  (simplify
    (extract_not (value_expr (extract_neg (value_with_ty a ty))))
  )
  (SimplifyRawResult.Expr (ExprData.Binary (BinaryOp.Sub) (arg_array2 a (make_one ty))))
)

;; Rules for UnaryOp.Neg
; -(-a) => a
(rule
  (simplify
    (extract_neg (value_expr (extract_neg a)))
  )
  (make_result a)
)
; -(!a) => a + 1
(rule
  (simplify
    (extract_neg (value_expr (extract_not (value_with_ty a ty))))
  )
  (SimplifyRawResult.Expr (ExprData.Binary (BinaryOp.Add) (arg_array2 a (make_one ty))))
)
